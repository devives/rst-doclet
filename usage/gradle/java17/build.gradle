plugins {
    id 'java'
}

group = 'com.devives'
version = '0.2.0'

compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

javadoc.options.encoding = "UTF-8"
javadoc.failOnError = false

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
        vendor = JvmVendorSpec.BELLSOFT
    }
}

sourceSets {
    main {
        java {
            srcDirs './../../../samples/src/main/java8/', './../../../samples/src/main/java11/', './../../../samples/src/main/java17/'
        }
    }
}

repositories {
    mavenLocal()
//    mavenCentral()
}

configurations {
    rstDoclet
}

dependencies {
    rstDoclet("com.devives:devive-rst-doclet-jdk17-all:0.3.0")
}

List<String> exportsList = [
        "--add-exports=jdk.compiler/com.sun.tools.javac.api=ALL-UNNAMED",
        "--add-exports=jdk.compiler/com.sun.tools.javac.file=ALL-UNNAMED",
        "--add-exports=jdk.compiler/com.sun.tools.javac.main=ALL-UNNAMED",
        "--add-exports=jdk.compiler/com.sun.tools.javac.model=ALL-UNNAMED",
        "--add-exports=jdk.compiler/com.sun.tools.javac.parser=ALL-UNNAMED",
        "--add-exports=jdk.compiler/com.sun.tools.javac.processing=ALL-UNNAMED",
        "--add-exports=jdk.compiler/com.sun.tools.javac.tree=ALL-UNNAMED",
        "--add-exports=jdk.compiler/com.sun.tools.javac.util=ALL-UNNAMED",
        "--add-exports=jdk.compiler/com.sun.tools.javac.code=ALL-UNNAMED",
        "--add-exports=jdk.compiler/com.sun.tools.javac.comp=ALL-UNNAMED",
        "--add-exports=jdk.compiler/com.sun.tools.doclint=ALL-UNNAMED",
        '--add-exports=jdk.javadoc/jdk.javadoc.internal=ALL-UNNAMED',
        '--add-exports=jdk.javadoc/jdk.javadoc.internal.tool=ALL-UNNAMED',
        '--add-exports=jdk.javadoc/jdk.javadoc.internal.doclint=ALL-UNNAMED',
        '--add-exports=jdk.javadoc/jdk.javadoc.internal.doclets.toolkit=ALL-UNNAMED',
        '--add-exports=jdk.javadoc/jdk.javadoc.internal.doclets.toolkit.builders=ALL-UNNAMED',
        '--add-exports=jdk.javadoc/jdk.javadoc.internal.doclets.toolkit.taglets=ALL-UNNAMED',
        '--add-exports=jdk.javadoc/jdk.javadoc.internal.doclets.toolkit.util=ALL-UNNAMED',
        '--add-exports=jdk.javadoc/jdk.javadoc.internal.doclets.formats.html=ALL-UNNAMED',
        '--add-exports=jdk.javadoc/jdk.javadoc.internal.doclets.formats.html.markup=ALL-UNNAMED',
        '--add-exports=jdk.javadoc/jdk.javadoc.internal.doclets.toolkit.util.links=ALL-UNNAMED',
        '--add-exports=jdk.javadoc/jdk.javadoc.internal.doclets.toolkit.util.links=ALL-UNNAMED'
]

tasks.register('clean-javadoc2rst', Delete) {
    group = 'documentation'
    delete file("$docsDir/javadoc2rst")
}

tasks.register('javadoc2rst', Javadoc) {
    dependsOn('clean-javadoc2rst')
    description = 'Generate rst files based on javadoc comments in code.'
    group = 'documentation'
    source = sourceSets.main.allJava
    classpath = configurations.compileClasspath
    destinationDir = file("$docsDir/javadoc2rst")
    options.docletpath = configurations.rstDoclet.files.asType(List)
    options.doclet = "com.devives.rstdoclet.RstDoclet"
    options.encoding = "UTF-8"
    options.showFromPackage()
    failOnError = false
    (options as CoreJavadocOptions).addStringOption("packageindexfilename", "package-index")
    (options as CoreJavadocOptions).setJFlags(exportsList)
}

tasks.register('clean-sphinx-source', Delete) {
    group = 'documentation-sphinx'
    delete file("$projectDir/sphinx/source/com/")
}
tasks.register('copy-rst-to-sphinx-source', Copy) {
    dependsOn(javadoc2rst, 'clean-sphinx-source')
    description = 'Coping rst-files, generated by task `javadoc2rst`, to the sphinx working directory.'
    group = 'documentation-sphinx'
    from(file("$docsDir/javadoc2rst"))
    into(file("$projectDir/sphinx/source"))
    println("Copy files from '" + file("$docsDir/javadoc2rst") + "' to '" + file("$projectDir/sphinx/source" + "'."))
    exclude('**/com/devives/samples/internals/**')
}

tasks.register('sphinx-clean', Exec) {
    description = 'Clean Sphinx\'s `build` directory.'
    group = 'documentation-sphinx'
    workingDir = file('./sphinx/')
    // on windows:
    commandLine = ['cmd', '/c', 'make.bat']
    // on linux
    // commandLine './make.sh'
    args('clean')
}

tasks.register('sphinx-make-html', Exec) {
    dependsOn('copy-rst-to-sphinx-source', 'sphinx-clean')
    description = 'Execute sphinx\'s `make.bat html` for generated rts-files.'
    group = 'documentation-sphinx'
    workingDir = file('./sphinx/')
    // on windows:
    commandLine = ['cmd', '/c', 'make.bat']
    // on linux
    // commandLine './make.sh'
    args('html')
}
